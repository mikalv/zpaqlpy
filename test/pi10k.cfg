(pi10k.cfg 2016 Kai LÃ¼ke and Matt Mahoney
instead of generating the digits in pcomp phase, use the next expected digit as context
To compress: zpaqd cinst pi10k.cfg pi10k.zpaq pi10000.txt)

comp 0 14 0 0 1  (2^14 > 10000)
  0 cm 13 0
hcomp
  ifnot (only first run)
    (Compute pi to 10000 digits in M using the formula:
       pi=4; for (d=r1*20/3;d>0;--d) pi=pi*d/(2*d+1)+2;
     where r1 is the number of base 100 digits.
     The precision is 1 bit per iteration so 20/3
     is slightly more than the log2(100) we need.)
    a= 100 a*=a r=a 1 (r1 = digits base 100)
    a*= 7 d=a (d = n iterations)
    *b= 4 (M=4)
    do

      (multiply M *= d, carry in c)
      b=r 1 c=0
      do
        b--
        a=*b a*=d a+=c c=a a%= 10 *b=a
        a=c a/= 10 c=a
      a=b a> 0 while

      (divide M /= (2d+1), remainder in c)
      a=d a+=d a++ d=a
      do
        a=c a*= 10 a+=*b c=a a/=d *b=a
        a=c a%=d c=a
      a=r 1 b++ a>b while
      a=d a>>= 1 d=a

      (add 2)
      b=0 a= 2 a+=*b *b=a
    d-- a=d a== 0 until
    halt
  endif
  a=*b a<<= 9 *d=a b++ (set context for expected digit taken from M)
  halt
end
